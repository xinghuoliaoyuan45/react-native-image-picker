import groovy.json.JsonSlurper

def computeVersionName() {
    // dynamically retrieve version from package.json
    def slurper = new JsonSlurper()
    def json = slurper.parse(file('../package.json'), "utf-8")
    return json.version
}

buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.+'
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName computeVersionName()
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    mavenCentral()
    maven {
        url "$projectDir/../Example/node_modules/react-native/android"
    }
    maven {
        url "$projectDir/../../react-native/android"
    }
    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
}

dependencies {
    compile "com.facebook.react:react-native:+"  // From node_modules
    compile('com.github.bumptech.glide:glide:4.1.1') {
        exclude(group: 'com.google.android', module: 'support-v4')
    }
    compile 'com.android.support:support-v4:26.1.0'
    compile 'com.google.code.gson:gson:2.2.4'

}
